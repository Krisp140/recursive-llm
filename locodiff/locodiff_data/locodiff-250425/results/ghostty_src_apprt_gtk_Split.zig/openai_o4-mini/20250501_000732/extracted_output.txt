/// Split represents a surface split where two surfaces are shown side-by-side
/// within the same window either vertically or horizontally.
const Split = @This();

const std = @import("std");
const Allocator = std.mem.Allocator;
const assert = std.debug.assert;

const gobject = @import("gobject");
const gtk = @import("gtk");

const apprt = @import("../../apprt.zig");
const font = @import("../../font/main.zig");
const CoreSurface = @import("../../Surface.zig");

const Surface = @import("Surface.zig");
const Tab = @import("Tab.zig");

const log = std.log.scoped(.gtk);

pub const Orientation = enum {
    horizontal,
    vertical,

    pub fn fromDirection(direction: apprt.action.SplitDirection) Orientation {
        return switch (direction) {
            .right, .left => .horizontal,
            .down, .up => .vertical,
        };
    }

    pub fn fromResizeDirection(direction: apprt.action.ResizeSplit.Direction) Orientation {
        return switch (direction) {
            .up, .down => .vertical,
            .left, .right => .horizontal,
        };
    }
};

paned: *gtk.Paned,
container: Surface.Container,
orientation: Orientation,

top_left: Surface.Container.Elem,
bottom_right: Surface.Container.Elem,

pub fn create(
    alloc: Allocator,
    sibling: *Surface,
    direction: apprt.action.SplitDirection,
) !*Split {
    var split = try alloc.create(Split);
    errdefer alloc.destroy(split);
    try split.init(sibling, direction);
    return split;
}

pub fn init(
    self: *Split,
    sibling: *Surface,
    direction: apprt.action.SplitDirection,
) !void {
    // If our sibling is too small to be split in half then we don't
    // allow the split to happen. This avoids a situation where the
    // split becomes too small.
    {
        const multiplier = 4;
        const size = &sibling.core_surface.size;
        const small = switch (direction) {
            .right, .left => size.screen.width < size.cell.width * multiplier,
            .down, .up => size.screen.height < size.cell.height * multiplier,
        };
        if (small) return error.SplitTooSmall;
    }

    // Create the new child surface for the other direction.
    const alloc = sibling.app.core_app.alloc;
    var surface = try Surface.create(alloc, sibling.app, .{
        .parent = &sibling.core_surface,
    });
    errdefer surface.destroy(alloc);
    sibling.dimSurface();
    sibling.setSplitZoom(false);

    // Create the actual GTKPaned, attach the proper children.
    const orientation: gtk.Orientation = switch (direction) {
        .right, .left => .horizontal,
        .down, .up => .vertical,
    };
    const paned = gtk.Paned.new(orientation);
    errdefer paned.unref();
    paned.ref();

    // Update all of our containers to point to the right place.
    const container = sibling.container;
    const tl: *Surface = undefined;
    const br: *Surface = undefined;
    switch (direction) {
        .right, .down => {
            sibling.container = .{ .split_tl = &self.top_left };
            surface.container = .{ .split_br = &self.bottom_right };
            @setLocal(tl, sibling);
            @setLocal(br, surface);
        },
        .left, .up => {
            sibling.container = .{ .split_br = &self.bottom_right };
            surface.container = .{ .split_tl = &self.top_left };
            @setLocal(tl, surface);
            @setLocal(br, sibling);
        },
    }

    self.* = .{
        .paned = paned,
        .container = container,
        .top_left = .{ .surface = tl },
        .bottom_right = .{ .surface = br },
        .orientation = Orientation.fromDirection(direction),
    };

    // Replace the previous containers element with our split. This allows a
    // non-split to become a split, a split to become a nested split, etc.
    container.replace(.{ .split = self });

    // Update our children so that our GL area is properly added to the paned.
    self.updateChildren();

    // The new surface should always grab focus
    surface.grabFocus();
}

pub fn destroy(self: *Split, alloc: Allocator) void {
    self.top_left.deinit(alloc);
    self.bottom_right.deinit(alloc);
    self.paned.unref();
    alloc.destroy(self);
}

pub fn removeTopLeft(self: *Split) void {
    removeChild(self, self.top_left, self.bottom_right);
}

pub fn removeBottomRight(self: *Split) void {
    removeChild(self, self.bottom_right, self.top_left);
}

fn removeChild(
    self: *Split,
    remove: Surface.Container.Elem,
    keep: Surface.Container.Elem,
) void {
    const window = self.container.window() orelse return;
    const alloc = window.app.core_app.alloc;

    self.removeChildren();

    keep.grabFocus();

    remove.deinit(alloc);
    alloc.destroy(self);
}

pub fn moveDivider(
    self: *Split,
    direction: apprt.action.ResizeSplit.Direction,
    amount: u16,
) void {
    const min_pos = 10;
    const pos = self.paned.getPosition();
    const new = switch (direction) {
        .up, .left => @max(pos - amount, min_pos),
        .down, .right => new_pos: {
            const max_pos: u16 = @as(u16, @intFromFloat(self.maxPosition())) - min_pos;
            break :new_pos @min(pos + amount, max_pos);
        },
    };
    self.paned.setPosition(new);
}

/// Equalize the splits in this split panel. Each split is equalized based on
/// its weight, i.e. the number of Surfaces it contains.
///
/// It works recursively by equalizing the children of each split.
///
/// It returns this split's weight.
pub fn equalize(self: *Split) f64 {
    const top_left_weight = self.top_left.equalize();
    const bottom_right_weight = self.bottom_right.equalize();
    const weight = top_left_weight + bottom_right_weight;
    const ratio = top_left_weight / weight;
    self.paned.setPosition(@intFromFloat(self.maxPosition() * ratio));
    return weight;
}

// maxPosition returns the maximum position of the GtkPaned, which is the
// "max-position" attribute.
fn maxPosition(self: *Split) f64 {
    var value: gobject.Value = std.mem.zeroes(gobject.Value);
    defer value.unset();
    _ = value.init(gobject.ext.types.int);
    self.paned.as(gobject.Object).getProperty("max-position", &value);
    return @floatFromInt(value.getInt());
}

// This replaces the element at the given pointer with a new element.
// The ptr must be either top_left or bottom_right (asserted in debug).
// The memory of the old element must be freed or otherwise handled by
pub fn replace(
    self: *Split,
    ptr: *Surface.Container.Elem,
    new: Surface.Container.Elem,
) void {
    assert(&self.top_left == ptr or &self.bottom_right == ptr);
    ptr.* = new;
    const pos = self.paned.getPosition();
    defer self.paned.setPosition(pos);
    self.updateChildren();
}

/// Update the paned children to represent the current state.
/// This should be called anytime the top/left or bottom/right
/// element is changed.
pub fn updateChildren(self: *const Split) void {
    self.removeChildren();
    self.paned.setStartChild(self.top_left.widget());
    self.paned.setEndChild(self.bottom_right.widget());
}

/// A mapping of direction to the element (if any) in that direction.
pub const DirectionMap = std.EnumMap(
    apprt.action.GotoSplit,
    ?*Surface,
);

pub const Side = enum { top_left, bottom_right };

pub fn directionMap(self: *const Split, from: Side) DirectionMap {
    var result = DirectionMap.initFull(null);

    if (self.directionPrevious(from)) |prev| {
        result.put(.previous, prev.surface);
        if (!prev.wrapped) {
            result.put(.up, prev.surface);
            result.put(.left, prev.surface);
        }
    }

    if (self.directionNext(from)) |next| {
        result.put(.next, next.surface);
        if (!next.wrapped) {
            result.put(.down, next.surface);
        }
    }

    if (self.directionLeft(from)) |left| {
        result.put(.left, left);
    }

    if (self.directionRight(from)) |right| {
        result.put(.right, right);
    }

    return result;
}

fn directionLeft(self: *const Split, from: Side) ?*Surface {
    switch (from) {
        .bottom_right => {
            switch (self.orientation) {
                .horizontal => return self.top_left.deepestSurface(.bottom_right),
                .vertical => return directionLeft(
                    self.container.split() orelse return null,
                    .bottom_right,
                ),
            }
        },
        .top_left => return directionLeft(
            self.container.split() orelse return null,
            .bottom_right,
        ),
    }
}

fn directionRight(self: *const Split, from: Side) ?*Surface {
    switch (from) {
        .top_left => {
            switch (self.orientation) {
                .horizontal => return self.bottom_right.deepestSurface(.top_left),
                .vertical => return directionRight(
                    self.container.split() orelse return null,
                    .top_left,
                ),
            }
        },
        .bottom_right => return directionRight(
            self.container.split() orelse return null,
            .top_left,
        ),
    }
}

fn directionPrevious(self: *const Split, from: Side) ?struct {
    surface: *Surface,
    wrapped: bool,
} {
    switch (from) {
        .bottom_right => return .{
            .surface = self.top_left.deepestSurface(.bottom_right) orelse return null,
            .wrapped = false,
        },
        .top_left => {
            const parent = self.container.split() orelse return .{
                .surface = self.bottom_right.deepestSurface(.bottom_right) orelse return null,
                .wrapped = true,
            };
            const side = self.container.splitSide() orelse return null;
            return switch (side) {
                .top_left => parent.directionPrevious(.top_left),
                .bottom_right => parent.directionPrevious(.bottom_right),
            };
        },
    }
}

fn directionNext(self: *const Split, from: Side) ?struct {
    surface: *Surface,
    wrapped: bool,
} {
    switch (from) {
        .top_left => return .{
            .surface = self.bottom_right.deepestSurface(.top_left) orelse return null,
            .wrapped = false,
        },
        .bottom_right => {
            const parent = self.container.split() orelse return .{
                .surface = self.top_left.deepestSurface(.top_left) orelse return null,
                .wrapped = true,
            };
            const side = self.container.splitSide() orelse return null;
            return switch (side) {
                .top_left => parent.directionNext(.top_left),
                .bottom_right => parent.directionNext(.bottom_right),
            };
        },
    }
}

fn deepestSurface(self: *const Split, side: Side) *Surface {
    return switch (side) {
        .bottom_right => switch (self.bottom_right) {
            .surface => |s| s,
            .split => |s| s.deepestSurface(.bottom_right),
        },
        .top_left => switch (self.top_left) {
            .surface => |s| s,
            .split => |s| s.deepestSurface(.top_left),
        },
    };
}

pub fn detachTopLeft(self: *const Split) void {
    self.paned.setStartChild(null);
}

pub fn detachBottomRight(self: *const Split) void {
    self.paned.setEndChild(null);
}

fn removeChildren(self: *const Split) void {
    self.detachTopLeft();
    self.detachBottomRight();
}